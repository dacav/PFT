#!/bin/perl

use strict;
use warnings;
use v5.16;
use utf8;

use Encode;
use Encode::Locale;
use File::Spec;
use version 0.77;

binmode STDIN,  ":encoding(console_in)"  if -t STDIN;
binmode STDOUT, ":encoding(console_out)" if -t STDOUT;
binmode STDERR, ":encoding(console_out)" if -t STDERR;


my $basedir = encode locale => 'lib';

-e $basedir or do {
    say "Run me from project root";
    exit 1
};

my($act, $par) = @ARGV;

sub nope {
    say "Error: @_" if @_;
    say "Usage: ";
    say "  $0 list";
    say "  $0 bump major";
    say "  $0 bump minor";
    say "  $0 bump patch";
    exit 1
};

defined($act) or nope 'no action';
$act =~ /^(?:list|bump|-*h(?:elp)?)$/ or nope 'invalid action';
$act =~ /-*h(?:elp)/ and nope;

if ($act eq 'bump') {
    defined $par or nope;
    $par =~ /(?:major|minor|patch)/ or nope 'invalid bump';
}

my @modules;

# Scan for all modules.
do {
    my @todo = $basedir;
    while (@todo) {
        my $d = shift @todo;
        opendir my $dh, $d or die "Cannot open $_: $!";
        my @sub = File::Spec->no_upwards(readdir $dh);

        push @todo, grep -d, map File::Spec->catdir($d, $_), @sub;
        push @modules, map File::Spec->catfile($d, $_), grep /\.pm$/, @sub;
    }
};

if ($act eq 'list') {
    foreach (@modules) {
        open my $fin, '<:encoding(locale)', $_ or die "Cannot open $_: $!";
        local $_; while (<$fin>) {
            chomp;
            /\s*package\s+(\S+)\s+(v[\d\.]+)\s*;/ or next;
            say "$1 $2";
            last;
        }
        close $fin;
    }
} else {
    my $new_version;

    # TODO Check index is clean;

    my @rollback_cbs;
    my $rollback = sub {
        select STDOUT;

        $_->() foreach @rollback_cbs;

        system git => checkout => '.'
            and die "PANIC, cannot git checkout for rollback. Error was: @_";
        die @_
    };

    for my $orig (@modules) {
        my $fix = "$orig.fix";

        open my $fin, '<:encoding(locale)', $orig or $rollback->("Cannot open $orig: $!");
        open my $fout, '>:encoding(locale)', $fix or $rollback->("Cannot open $fix: $!");
        push @rollback_cbs, sub { unlink $fix };

        select $fout;
        local $" = '.';

        local $_; while (<$fin>) {
            if (my($cur, @mmp) = /\s*package\s+\S+\s+v((\d+)\.(\d+)\.(\d+))\s*;/) {
                $mmp[$par eq 'major' ? 0 : $par eq 'minor' ? 1 : 2] ++;

                my $nv = version->parse("@mmp");
                if (defined $new_version) {
                    if ($new_version != $nv) {
                        $rollback->("Mismatching version for $orig, $nv != $new_version")
                    }
                } else {
                    $new_version = $nv;
                }

                s/\Q$cur\E/@mmp/;
            }
            print or $rollback->("Cannot print in $fix: $!")
        }
        close $fin;
        close $fout;
        rename $fix, $orig;
        pop @rollback_cbs;
    }

    select STDOUT;
    system git => commit => -a => -m => "Bumped $par"
        and $rollback->('Cannot commit');

    system git => tag => -s => -m => "Release $new_version" => "v$new_version"
        and say "Everything ok, except cannot tag!";
}
